apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'distribution'

def groupId = 'com.lingcreative'
def artifactId = 'gradle-initializer'

sourceCompatibility = 1.7
version = '1.0'

jar {
    baseName = artifactId
    manifest {
        attributes 'Implementation-Title': 'Gradle Initializer',
                   'Implementation-Version': version,
                   'Main-Class': 'com.lingcreative.gradle.wrapper.GradleWrapperInitializer',
                   'Class-Path': './gradle-wrapper.jar'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile files("gradle/wrapper/gradle-wrapper.jar")
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

distributions {
    def jarCopySpec = project.copySpec {
        from  { 'gradle/wrapper/gradle-wrapper.jar' }
        from ( jar.archivePath ) {
            rename '.*', "${artifactId}.jar"
        }
        into 'initializer'
    }
    def scriptCopySpec = project.copySpec {
        from('src/main/dist') {
            include '*'
        }
    }
    main {
        baseName = 'gradle-initializer'
        contents {
            with jarCopySpec
        }
    }
    withGradles {
        baseName = 'gradle-initializer-with-gradles'
        contents {
            with(scriptCopySpec, jarCopySpec)
            from("${System.getProperty('user.home')}${File.separator}gradle-distributions") {
                rename '(.*)', 'distributions/$1'
            }
        }
    }
}

distTar.dependsOn jar
distZip.dependsOn jar
withGradlesDistTar.dependsOn jar
withGradlesDistZip.dependsOn jar